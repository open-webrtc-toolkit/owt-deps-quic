# Copyright (C) <2020> Intel Corporation
#
# SPDX-License-Identifier: Apache-2.0

import("//chrome/process_version_rc_template.gni")
import("//testing/test.gni")

config("owt_quic_transport_config") {
  include_dirs = [
    "./",
    "api",
  ]
  defines = [ "OWT_QUIC_LIBRARY_IMPL" ]
}

source_set("owt_quic_transport_impl") {
  deps = [
    ":generate_version_info",
    "//base",
    "//net",
    "//net:simple_quic_tools",
    "//net/third_party/quiche:simple_quic_tools_core",
    "//third_party/boringssl",
  ]
  sources = [
    "api/owt/quic/quic_definitions.h",
    "api/owt/quic/quic_transport_client_interface.h",
    "api/owt/quic/quic_transport_factory.h",
    "api/owt/quic/quic_transport_server_interface.h",
    "api/owt/quic/version.h",
    "impl/proof_source_owt.cc",
    "impl/proof_source_owt.h",
    "impl/quic_transport_factory_impl.cc",
    "impl/quic_transport_factory_impl.h",
    "impl/quic_transport_owt_client_impl.cc",
    "impl/quic_transport_owt_client_impl.h",
    "impl/quic_transport_owt_server_dispatcher.cc",
    "impl/quic_transport_owt_server_dispatcher.h",
    "impl/quic_transport_owt_server_impl.cc",
    "impl/quic_transport_owt_server_impl.h",
    "impl/quic_transport_owt_server_session.cc",
    "impl/quic_transport_owt_server_session.h",
    "impl/quic_transport_stream_impl.cc",
    "impl/quic_transport_stream_impl.h",
    "impl/version.cc",
  ]
  configs += [ ":owt_quic_transport_config" ]
}

if (is_win) {
  process_version_rc_template("owt_quic_transport_dll_version") {
    deps = [ ":update_last_change" ]
    template_file = "resources/owt_version.rc.version"
    sources = [
      "$target_gen_dir/lastchange",
      "VER",
    ]
    output = "$target_gen_dir/owt_quic_transport_dll_version.rc"
  }
}

action("update_last_change") {
  script = "//build/util/lastchange.py"
  args = [
    "-s",
    rebase_path("//owt"),
    "--filter",
    ".*",
    "-o",
    rebase_path("$target_gen_dir/lastchange"),
  ]
  outputs = [ "$target_gen_dir/lastchange" ]
}

process_version("generate_version_info") {
  deps = [ ":update_last_change" ]
  template_file = "impl/version_info_values.h.template"
  sources = [
    "$target_gen_dir/lastchange",
    "VER",
  ]
  output = "$target_gen_dir/version_info_values.h"
}

shared_library("owt_quic_transport") {
  deps = [ ":owt_quic_transport_impl" ]
  configs += [ ":owt_quic_transport_config" ]
  if (is_win) {
    deps += [ ":owt_quic_transport_dll_version" ]
    sources = [ "impl/dllmain.cc" ]
  }
}

test("owt_quic_transport_tests") {
  testonly = true
  sources = [
    "impl/proof_source_owt_unittest.cc",
    "impl/quic_transport_factory_impl_unittest.cc",
    "impl/tests/quic_transport_owt_end_to_end_test.cc",
    "impl/tests/run_all_unittests.cc",
    "impl/version_unittest.cc",
  ]
  configs += [
    "//build/config:precompiled_headers",
    ":owt_quic_transport_config",
  ]
  deps = [
    ":owt_quic_transport_impl",
    "//net:quic_test_tools",
    "//net:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

if (is_win) {
  test("owt_quic_transport_dll_tests") {
    testonly = true
    sources = [
      "impl/dllmain_unittest.cc",
      "impl/tests/run_all_unittests.cc",
    ]
    deps = [
      ":owt_quic_transport",
      "//net:quic_test_tools",
      "//net:test_support",
      "//testing/gmock",
      "//testing/gtest",
    ]
    configs += [ ":owt_quic_transport_config" ]
  }
}
